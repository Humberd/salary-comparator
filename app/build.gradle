plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics)
    alias(libs.plugins.protobuf)
}

android {
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "pl.humberd.salary_comparator"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 11
        versionName "1.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'pl.humberd.salary_comparator'
}

dependencies {
    // AndroidX Core
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat

    // Material Design
    implementation libs.google.material

    // Compose
    implementation libs.bundles.compose
    debugImplementation libs.compose.ui.tooling

    // Lifecycle
    implementation libs.bundles.lifecycle

    // Activity & Navigation
    implementation libs.activity.compose
    implementation libs.navigation.compose

    // DataStore
    implementation libs.bundles.datastore

    // Protobuf
    implementation libs.bundles.protobuf

    // Accompanist
    implementation libs.bundles.accompanist

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase

    // Retrofit
    implementation libs.bundles.retrofit

    // Coroutines
    implementation libs.kotlinx.coroutines.android

    // Third-party
    implementation libs.flagkit
}

protobuf {
    protoc {
        artifact = libs.protobuf.protoc.get().toString()
    }

    plugins {
        kotlin {}
        java {}
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                kotlin {}
                java {}
            }
        }
    }
}
